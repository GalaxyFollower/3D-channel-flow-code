!wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww!
!---------------------------------------------------------------------!
!                        INPUT PARALLEL FILE                          !
!                      Miguel Angel Uh Zapata                         !
!                             Jun 2014                                !
!---------------------------------------------------------------------!
!wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww!


      SUBROUTINE parallel_input_global

!---------------------------------------------------------------------!
!                                                                     !
!    This program reads the data files:                               !
!                  -   data.txt                                       !
!                  -   dataChaco.txt                                  !
!    corresponding to the data of the physical domain generated by    !
!    our mesh generator and by Chaco program and save it in global    !
!    data variables.                                                  !
!                                                                     !
!---------------------------------------------------------------------!
!                                                                     !
!    Output variables:                                                !
!   _______________________________________________________________   !
!  |       Name       |    Dimension    |       Description        |  !
!  |__________________|_________________|__________________________|  !
!  |                  |                 |                          |  !
!  | <--- No_vp_global|(N_CELL0global,3)| Neighbors vertices       |  !
!  | <--- No_cp_global|(N_CELL0global,3)| Neighbors cell-centers   |  !
!  | <--- nbe_global  |(N_CELL0global)  | Type of cell-center      |  !
!  | <--- xv_global   |(N_VERTglobal)   | x-vertex coordinate      |  !
!  | <--- yv_global   |(N_VERTglobal)   | y-vertex coordinate      |  !
!  | <--- zbv_global  |(N_VERTglobal)   | Water levels             |  !
!  |__________________|_________________|__________________________|  !
!  |                  |                 |                          |  !
!  | <--- TagParallel |(N_CELL0global)  | Subdomain tag (parallel) |  !
!  |__________________|_________________|__________________________|  !
!                                                                     !
!---------------------------------------------------------------------!

!*********************************************************************!
!                                                                     !
!                           Definitions                               !
!                                                                     !
!*********************************************************************!
!     ____________________________________
!    |                                    |
!    |     Keys and common parameters     |
!    |____________________________________|

#     include "cppdefs.h"
!     ====================================
!     ==========  SEQUENTIAL =============
#     ifndef KeyParallel
            USE geometry
            implicit none
#     endif
!     ====================================
!     =====  START PARALLEL OPTION =======
#     ifdef KeyParallel
            USE parallel
            USE geometry
            implicit none
#     endif
!     =============== END ================
!     ====================================
!     ____________________________________
!    |                                    |
!    |   Declaration of local variables   |
!    |____________________________________|

      integer :: ii,s,s0,i0,j0,k0,m,sm,tag
      integer :: Jini,Jfin,Iini,Ifin
      integer :: Ielem,Jelem,Jvertex,Ivertex,kelem
      integer :: jc,jc1,jc2,jc3
      integer :: jv,jv1,jv2,jv3
      integer :: elem,irec,NNbefore
      integer :: NChaco
      real*8  :: xxc,yyc
!     ------------------------------
      character*80 title
      character*36 filen
      character*39 filen2

!     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#     ifdef KeyDbg
           print*, '      ----> Begin subroutine: parallel_input'
#     endif
!     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

!*********************************************************************!
!                                                                     !
!                   Read data files:  data.txt                        !
!                                     dataChaco.txt                   !
!                                                                     !
!*********************************************************************!

      allocate(No_vp_global(N_CELL0global,3))
      allocate(No_cp_global(N_CELL0global,3))
      allocate(xc_global(N_CELL0global))
      allocate(yc_global(N_CELL0global))
      allocate(nbe_global(N_CELL0global))
      allocate(nbev_global(N_VERTglobal))
      allocate(xv_global(N_VERTglobal))
      allocate(yv_global(N_VERTglobal))
      allocate(zbv_global(N_VERTglobal))
!     --------------------------------------------------------------
!     allocate global variable for parallel mode
      allocate(vcount_global(N_VERTglobal), &
               TagXV_global(N_VERTglobal),  &
               TagYV_global(N_VERTglobal))
!     -------------------------------------
!     To use in Newmann BC
!     'vcount_global'assign value in parameter.F90
       DO nv=1,N_VERTglobal
          vcount_global(nv) = 0
          TagXV_global(nv)  = 0
          TagYV_global(nv)  = 0
       ENDDO
!     --------------------------------------------------------------
!     for periodic option
#     ifdef KeyTESTpBC
      allocate(vpair_global(N_VERTglobal), &
               vedge_global(N_VERTglobal))
#     endif

74    format(A46)
85    format(20I7)
86    format(10F8.3)
87    format(2F15.5)
88    format(3I8)
89    format(I8)
!      ________________________________________________________
!     |                                                        |
!     |                Read data file: data.txt                |
!     |________________________________________________________|

!     ________________________________________________________
!     Open file

      open(22,file='data.txt',status='old')

!     ________________________________________________________
!     Read: Number of cell-center and vertex points
      read(22,74) title
      read(22,74) title
      read(22,74) title
!     ________________________________________________________
!     Read: Numbering of the cell vertices for each element

      read(22,74) title
      read(22,88) ((No_vp_global(i,j),j=1,3),i=1,N_CELL0global)
!     ________________________________________________________
!     Read: Coordinates of cell vertices
      read(22,74) title
      read(22,87) (xv_global(nv),yv_global(nv),nv=1,N_VERTglobal)

!     ________________________________________________________
!     Read: Numbering of the surrounding three cell centers

      read(22,74) title
      read(22,88) ((No_cp_global(i,j),j=1,3),i=1,N_CELL0global)
!     ________________________________________________________
!     Read: Bottom levels

      read(22,74) title
      read(22,86) (zbv_global(nv),nv=1,N_VERTglobal)
!     ________________________________________________________
!     Read: Type of cell (inside or boundary)

      read(22,74) title
      read(22,85) (nbe_global(i),i=1,N_CELL0global)
!     ________________________________________________________
!     Read: Type of vertex
      read(22,74) title
      read(22,85) (nbev_global(i),i=1,N_VERTglobal)
!     Close file
      close(22)

      N_BC = 0
      do nv=1,N_VERTglobal
        if(nbev_global(nv) .gt. 0) N_BC = N_BC + 1
      enddo
!     ---------------------------------------------------------
!     Assign physical bc for BlueKenue 2D Mesh
        if(ChooseBoundary .eq. 0) then
            do i=1,N_VERTglobal
                if(nbev_global(i) .eq. 1) then
                    nbev_global(i) = 1 ! top
                elseif(nbev_global(i) .eq. 2) then
                    nbev_global(i) = 1 ! bottom
                elseif(nbev_global(i) .eq. 3) then
                    nbev_global(i) = 4 ! right
                elseif(nbev_global(i) .eq. 4) then
                    nbev_global(i) = 3 ! left
                endif
            enddo
        else
            do i=1,N_VERTglobal
                if(nbev_global(i) .gt. 0)  nbev_global(i) = 1
            enddo
        endif

!      ________________________________________________________
!     |                                                        |
!     |                   Read the data file                   |
!     |________________________________________________________|

      allocate(TagParallel(N_CELL0global))
!     ________________________________________________________
!     Open file
!     COMMENT: should pay attention to the file name
!     when create chaco file
           NChaco = Nprocs
        if (NChaco.eq.2) then
            open(12,file='dataChaco02.txt',status='old')
        elseif (NChaco.eq.4) then
            open(12,file='dataChaco04.txt',status='old')
        elseif (NChaco.eq.8) then
            open(12,file='dataChaco08.txt',status='old')
        elseif (NChaco.eq.16)then
            open(12,file='dataChaco16.txt',status='old')
        elseif (NChaco.eq.32)then
            open(12,file='dataChaco32.txt',status='old')
        else
            open(12,file='dataChaco.txt',status='old')
        endif
!     ________________________________________________________
!     Read: Tags corresponding for each subdomain

      do i=1,N_CELL0global
         read(12,89) TagParallel(i)
      enddo
!     ________________________________________________________
!     Close file

      close(12)


!*********************************************************************!
!                                                                     !
!                             Finalization                            !
!                                                                     !
!*********************************************************************!

        IF (rang_topo.eq.0) THEN
            print*,'_________________________________________________________'
            print*,'                                                         '
            print*,'    ==================================================  '
            print*,'                   MPI: Reading data                     '
            print*,'    ==================================================  '
            print*,'                                                         '
            print*,'      -- data.txt      has been succefully read --       '
            print*,'      -- dataChaco.txt has been succefully read --       '
            print*,'                                                         '
            print*,'  Num. of vertices      :  N_VERTglobal  =',N_VERTglobal
            print*,'  Num. of cell-centers  :  N_CELL0global =',N_CELL0global
            print*,'  Num. of bc  vertices  :  N_BCglobal    =',N_BC
            print*,'                                                         '
        ENDIF

!     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#     ifdef KeyDbg
           print*, '      ----> End   subroutine: parallel_input'
#     endif
!     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

      RETURN
      END


!wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww!
!---------------------------------------------------------------------!
!                      	   END OF INPUT                               !
!---------------------------------------------------------------------!
!wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww!

